plugins {
    id 'idea'
    id "org.jetbrains.kotlin.jvm" version "$version_kotlin"
    id "org.jetbrains.kotlin.plugin.serialization" version "$version_kotlin"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.openapi.generator" version "6.2.0"
    id "de.undercouch.download" version "5.4.0"
}

apply plugin: 'distribution'

allprojects {
    /* Group name of our artifacts */
    group = 'org.vitrivr'

    /* Our current version, on dev branch this should always be release+1-SNAPSHOT */
    version = '0.15.1'

    /* Repositories for build script. */
    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }

    /* Project repositories for build script. */
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

subprojects {
    /* Common plugins. */
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'

    /* Compiler options for Kotlin. */
    def kotlinCompilerOptions = ["-Xcontext-receivers"]
    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = kotlinCompilerOptions
            jvmTarget = "1.8"
        }
    }

    /* Compiler options for Kotlin when running tests. */
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = kotlinCompilerOptions
            jvmTarget = "1.8"
        }
    }

    compileTestFixturesKotlin {
        kotlinOptions {
            freeCompilerArgs = kotlinCompilerOptions
            jvmTarget = "1.8"
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    /** Increase heap size for all unit tests. */
    test {
        minHeapSize = "256M"
        maxHeapSize = "2G"
    }

    javadoc {
        options.addStringOption("Xdoclint:none", "-quiet")
        options.memberLevel = JavadocMemberLevel.PRIVATE
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption("html5", true)
        }
    }

    /* Important warnings that are currently missing simply because there are too many: "cast", "rawtypes". Ideally, "all" should be used in the future. */
    def javaCompilerOptions = []
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += javaCompilerOptions
    }
    compileTestJava.options.compilerArgs += javaCompilerOptions

    /* Common dependencies. */
    dependencies {
        ///// Kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: version_kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: version_kotlin
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: version_kotlinx_coroutines
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json-jvm', version: version_kotlinx_json_jvm

        ///// Cottontail DB Driver + gRPC dependencies
        implementation group: 'org.vitrivr', name: 'cottontaildb-proto', version: version_cottontaildb_driver
        implementation group: 'io.grpc', name: 'grpc-all', version: version_grpc
        implementation group: 'io.grpc', name: 'grpc-kotlin-stub', version: version_grpc_kotlin
        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: version_protobuf

        ////// Log4j2 & SLF4j
        implementation group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: version_log4j2

        ///// JUnit 5
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: version_junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: version_junit
        testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: version_junit_platform
    }
}

/* Generate common Cottontail DB distribution*/
distributions {
    main {
        contents {
            into('bin') {
                from { project(':cottontaildb-dbms').startScripts.outputs.files }
                from { project(':cottontaildb-cli').startScripts.outputs.files }
                fileMode = 0755
            }
            into('lib') {
                def libs = []
                libs << project(':cottontaildb-dbms').configurations.runtimeClasspath
                libs << project(':cottontaildb-cli').configurations.runtimeClasspath
                from libs
                from project(':cottontaildb-dbms').jar
                from project(':cottontaildb-cli').jar
            }
        }
    }
}

//// Change name for distribution to C(arrot)LI.
distZip.archiveFileName = 'cottontaildb-full.zip'
distTar.archiveFileName = 'cottontaildb-full.tar'

/// Variables used for Open API generation.
def fullOAS = 'http://localhost:7070/swagger-docs'
def oasFile = "${project.projectDir}/doc/oas.json"

/// Generates the openapi frontend bindings
openApiGenerate {
    /// Source command:
    /// openapi-generator generate -g typescript-angular -i http://localhost:8080/swagger-docs -o openapi --skip-validate-spec --additional-properties npmName=@dres-openapi/api,snapshot=true,ngVersion=9.1.0
    generateApiTests = false // No tests please
    generateModelTests = false // No tests please
    validateSpec = false // No validation please (as in command above)
    skipValidateSpec = true

    generatorName = 'typescript-angular'
    inputSpec = oasFile
    outputDir = file("${project.projectDir}/cottontaildb-ui-frontend/openapi").toString()
    configOptions = [
        npmName: '@thumper-openapi/api',
        ngVersion: '13.2.3',
        snapshot: 'true', /// I suggest to remove this, as soon as we automate this,
        enumPropertyNaming: 'original'
    ]
}

tasks.register('generateOAS', Download) {
    /* Requires DRES running on default port */
    def f = new File(oasFile)
    src fullOAS
    dest f
}