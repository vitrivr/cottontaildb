plugins {
    id "org.jetbrains.kotlin.jvm" version "$version_kotlin"
    id "org.jetbrains.kotlin.plugin.serialization" version "$version_kotlin"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'idea'
}

apply plugin: 'distribution'

allprojects {
    /* Group name of our artifacts */
    group = 'org.vitrivr'

    /* Our current version, on dev branch this should always be release+1-SNAPSHOT */
    version = '0.14.0'

    /* Repositories for build script. */
    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }

    /* Project repositories for build script. */
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }


    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.github.johnrengelman.shadow'
}

subprojects {
    /* Common plugins. */
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'java-library'

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions {
            freeCompilerArgs = ["-Xinline-classes"]
        }
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        options.addStringOption("Xdoclint:none", "-quiet")
        options.memberLevel = JavadocMemberLevel.PRIVATE
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption("html5", true)
        }
    }

    /* Important warnings that are currently missing simply because there are too many: "cast", "rawtypes". Ideally, "all" should be used in the future. */
    def enabledWarnings = ["-Xlint:deprecation", "-Xlint:empty", "-Xlint:overrides", "-Xlint:serial", "-Xlint:static", "-Xlint:unchecked", "-Xlint:varargs"]
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += enabledWarnings
    }
    compileTestJava.options.compilerArgs += enabledWarnings

    /* Common dependencies. */
    dependencies {
        ///// Kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: version_kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: version_kotlin
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: version_kotlinx_coroutines
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json-jvm', version: version_kotlinx_json_jvm

        ///// Cottontail DB Driver + gRPC dependencies
        implementation group: 'org.vitrivr', name: 'cottontaildb-proto', version: version_cottontaildb_driver
        implementation group: 'io.grpc', name: 'grpc-all', version: version_grpc
        implementation group: 'io.grpc', name: 'grpc-kotlin-stub', version: version_grpc_kotlin
        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: version_protobuf

        ////// Log4j2 & SLF4j
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: version_log4j2

        ///// JUnit 5
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: version_junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: version_junit
        testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: version_junit_platform
    }
}

/* Generate common Cottontail DB distribution*/
distributions {
    main {
        contents {
            into('bin') {
                from { project(':cottontaildb-dbms').startScripts.outputs.files }
                from { project(':cottontaildb-cli').startScripts.outputs.files }
                fileMode = 0755
            }
            into('lib') {
                def libs = []
                libs << project(':cottontaildb-dbms').configurations.runtimeClasspath
                libs << project(':cottontaildb-cli').configurations.runtimeClasspath
                from libs
                from project(':cottontaildb-dbms').jar
                from project(':cottontaildb-cli').jar
            }
        }
    }
}

//// Change name for distribution to C(arrot)LI.
distZip.archiveFileName = 'cottontaildb-full.zip'
distTar.archiveFileName = 'cottontaildb-full.tar'