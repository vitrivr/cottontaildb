group 'org.vitrivr'
version '1.0-SNAPSHOT'

buildscript {
    ext.luceneVersion = '7.7.1'
    ext.kotlinVersion = '1.3.60'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
        classpath "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

mainClassName = 'org.vitrivr.cottontail.CottontailKt'
applicationDefaultJvmArgs = ["-Xms2G", "-Xmx4G"]

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        proto {
            srcDirs += "$projectDir/cottontaildb-proto/src/main/protobuf"
        }
        kotlin {
            srcDirs += "$projectDir/build/generated/source/proto/main/java"
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated/source/proto"
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.29.0'
        }
    }
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.3'
    }
    generateProtoTasks {
        all()*.plugins { grpc {
            outputSubDir = 'java'
        }}
    }
}

dependencies {
    ///// MapDB dependency
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.8'

    ///// FastUtil
    compile group: 'it.unimi.dsi', name: 'fastutil', version: '8.3.1'

    /////// gRPC & Protobuf
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.12.2'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.29.0'

    ////// Log4J
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.2'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'

    ////// Dexecutor
    compile group: 'com.github.dexecutor', name: 'dexecutor-core', version: '2.1.1'

    ////// Lucene
    compile group: 'org.apache.lucene', name: 'lucene-core', version: luceneVersion
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: luceneVersion
    compile group: 'org.apache.lucene', name: 'lucene-codecs', version: luceneVersion
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: luceneVersion

    ////// CLIKT
    compile group: 'com.github.ajalt', name: 'clikt', version: '2.6.0'
    ////// JLine 3
    compile group: 'org.jline', name: 'jline', version: '3.15.0'

    ///// JUnit 5
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2'
    ///// Kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.5'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: '0.14.0'

    ///// Apache Commons Math Â» 3.6
    ///// https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}

shadowJar {
    mergeServiceFiles()
}

idea {
    module {
        downloadJavadoc = true

        excludeDirs += file('data')
    }
}

///// Default names for distributions
distZip.archiveName('cottontaildb-bin.zip')
distTar.archiveName('cottontaildb-bin.tar')
