/* Plugins. */
apply plugin: 'application'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'

/* Cottontail DB dependencies. */
dependencies {
    api project(':cottontaildb-core')

    ///// MapDB dependency
    implementation group: 'org.mapdb', name: 'mapdb', version: version_mapdb

    ///// Xodus dependency
    implementation group: 'org.jetbrains.xodus', name: 'xodus-openAPI', version: version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'xodus-environment', version: version_xodus
    implementation group: 'org.jetbrains.xodus', name: 'xodus-entity-store', version: version_xodus

    ///// Snappy
    implementation group: 'org.xerial.snappy', name: 'snappy-java', version: version_snappy

    ///// FastUtil
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: version_fastutil

    ///// Google Guava
    implementation group: 'com.google.guava', name: 'guava', version: version_guava

    ////// Log4j2 & SLF4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: version_log4j2

    ////// Lucene
    implementation group: 'org.apache.lucene', name: 'lucene-core', version: version_lucene
    implementation group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '8.11.1'
    implementation group: 'org.apache.lucene', name: 'lucene-analyzers-phonetic', version: '8.11.1'
    implementation group: 'org.apache.lucene', name: 'lucene-codecs', version: version_lucene
    implementation group: 'org.apache.lucene', name: 'lucene-queryparser', version: version_lucene
}

/* General application attributes. */
application {
    applicationName = 'cottontaildb'
    mainClassName = 'org.vitrivr.cottontail.CottontailKt'
    applicationDefaultJvmArgs = ["-Xms2G", "-Xmx4G", "--add-modules", "jdk.incubator.vector"]
}

/* Publication of Cottontail DB to Maven Central. */
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'cottontaildb'
            version = System.getenv().getOrDefault("MAVEN_PUBLICATION_VERSION", version.toString())
            from components.java
            pom {
                name = 'Cottontail DB'
                description = 'Cottontail DB as java library, for use in embedded mode.'
                url = 'https://github.com/vitrivr/cottontaildb/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/cottontaildb.git'
                    url = 'https://github.com/vitrivr/cottontaildb'
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                name = "OSSRH"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

shadowJar {
    mergeServiceFiles()
}

idea {
    module {
        downloadJavadoc = true
        excludeDirs += file('cottontaildb-data')
    }
}

// used to pass flags such as windows to test-runner
test {
    systemProperties(System.getProperties())
}

tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs '--enable-preview', '--add-modules', 'jdk.incubator.vector'
}

///// Default names for distributions
distZip.archiveFileName = 'cottontaildb-dbms.zip'
distTar.archiveFileName = 'cottontaildb-dbms.tar'

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}