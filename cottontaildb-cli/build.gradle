plugins {
    id "application"
    id "com.github.johnrengelman.shadow"
    id "org.jetbrains.kotlin.plugin.serialization" version "$version_kotlin"
}


/* C(arrot)LI dependencies. */
dependencies {
    api project(':cottontaildb-core')

    /* CLI & Terminal. */
    implementation group: 'org.jline', name: 'jline', version: version_jline
    implementation group: 'org.jline', name: 'jline-terminal-jna', version: version_jline
    implementation group: 'com.github.ajalt.clikt', name: 'clikt', version: version_clikt
    implementation group: 'com.jakewharton.picnic', name: 'picnic', version: version_picnic

    /* JSON, PROTOBUF and CSV support for kotlinx.serialization. */
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json-jvm', version: version_kotlinx_serialization
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-protobuf-jvm', version: version_kotlinx_serialization
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-cbor-jvm', version: version_kotlinx_serialization
    implementation group: 'de.brudaswen.kotlinx.serialization', name: 'kotlinx-serialization-csv', version: version_kotlinx_serialization_csv

    /* Apache Commons Compress. */
    implementation group: 'org.apache.commons', name: 'commons-compress', version: version_commons_compress

    /* Test Fixtures from Cottontail DB core. */
    testImplementation(testFixtures(project(":cottontaildb-dbms")))
}

/* General application attributes. */
application {
    applicationName = 'carrotli'
    applicationDefaultJvmArgs = ["-Xms512M", "-Xmx2G"]
    mainClassName = 'org.vitrivr.cottontail.CarrotliKt'
}

shadowJar {
    mergeServiceFiles()
}

idea {
    module {
        downloadJavadoc = true
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

//// Change name for distribution to C(arrot)LI.
distZip.archiveFileName = 'cottontaildb-cli.zip'
distTar.archiveFileName = 'cottontaildb-cli.tar'

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}