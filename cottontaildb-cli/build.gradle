/* Plugins. */
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

/* C(arrot)LI dependencies. */
dependencies {
    api project(':cottontaildb-core')

    /* CLI & Terminal. */
    implementation group: 'org.jline', name: 'jline', version: version_jline
    implementation group: 'org.jline', name: 'jline-terminal-jna', version: version_jline
    implementation group: 'com.github.ajalt.clikt', name: 'clikt', version: version_clikt
    implementation group: 'com.jakewharton.picnic', name: 'picnic', version: version_picnic

    /* CSV support .*/
    implementation group: 'com.github.doyaaaaaken', name: 'kotlin-csv-jvm', version: '1.3.0'

    /* JSON via GSON (cannot use Kotlinx Serialization here, because format is not strictly typed). */
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    /* Test Fixtures from Cottontail DB core.. */
    testImplementation(testFixtures(project(":cottontaildb-dbms")))
}

/* General application attributes. */
application {
    applicationName = 'carrotli'
    applicationDefaultJvmArgs = ["-Xms512M", "-Xmx1G"]
    mainClassName = 'org.vitrivr.cottontail.CarrotliKt'
}

shadowJar {
    mergeServiceFiles()
}

idea {
    module {
        downloadJavadoc = true
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

//// Change name for distribution to C(arrot)LI.
distZip.archiveFileName = 'cottontaildb-cli.zip'
distTar.archiveFileName = 'cottontaildb-cli.tar'

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}